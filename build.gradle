plugins {
    id 'java'
    id 'application'
}

group 'com.mygame'
version '1.0.0'

application {
    applicationName = 'JaimesAscent'
    mainClass = 'jme3test.jaimesascent.Main'
    // Mac OS with LWJGL 3 needs to be started with this JVM argument
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        applicationDefaultJvmArgs = ['-XstartOnFirstThread']
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

project.ext {
    jmeVer = '3.7.0-stable'
}

project(":assets") {
    apply plugin: "java"

    buildDir = rootProject.file("build/assets")

    sourceSets {
        main {
            resources {
                srcDir '.'
            }
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}

dependencies {
    
    // Core JME
    implementation "org.jmonkeyengine:jme3-core:$jmeVer"
    implementation "org.jmonkeyengine:jme3-desktop:$jmeVer"
    implementation "org.jmonkeyengine:jme3-effects:$jmeVer"
    
    // Mac OS with LWJGL 3 doesn't allow AWT/Swing
    if (!System.getProperty("os.name").toLowerCase().contains("mac")) {
        runtimeOnly "org.jmonkeyengine:jme3-awt-dialogs:$jmeVer"
    }
    runtimeOnly "org.jmonkeyengine:jme3-lwjgl3:$jmeVer" // lwjgl3
    
    // Suppress errors / warnings building in SDK
    runtimeOnly "org.jmonkeyengine:jme3-jogg:$jmeVer"
    runtimeOnly "org.jmonkeyengine:jme3-plugins:$jmeVer"
    
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:3.4.0-alpha6'
    
    // Additional Libraries
    implementation 'com.github.stephengold:Minie:8.2.0+big4'
    
    // GUI Libraries
    implementation 'com.simsilica:lemur:1.16.0'
    implementation 'com.simsilica:lemur-props:1.2.0'
    implementation 'com.simsilica:lemur-proto:1.13.0'
    runtimeOnly 'org.codehaus.groovy:groovy-jsr223:3.0.19'
    
    // Assets sub-project
    runtimeOnly project(':assets')
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

wrapper {
    gradleVersion = '8.4'
}